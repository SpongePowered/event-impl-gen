// Shared deployment configuration for the Sponge projects

apply plugin: 'maven-publish'

publishing {
    publications {
        /*pluginMaven(MavenPublication) {
            project.shadow.component(it)
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
        }*/

        withType(MavenPublication).configureEach {
            // artifactId = project.archivesBaseName
            pom {
                name = project.projectName
                description = project.description
                url = project.url

                scm {
                    url = "https://github.com/$project.organization/$project.name"
                    connection = "scm:git:https://github.com/$project.organization/${project.name}.git"
                    developerConnection = "scm:git:git@github.com:$project.organization/${project.name}.git"
                }

                issueManagement {
                    system = 'GitHub Issues'
                    url = "https://github.com/$project.organization/$project.name/issues"
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }
            }
        }
    }

    repositories {
        // This is set by the build server when deploying to the repo
        if (project.hasProperty('spongeSnapshotRepo') || project.hasProperty('spongeReleaseRepo')) {
            maven {
                if (project.version.contains("-SNAPSHOT")) {
                    url = project.spongeSnapshotRepo
                } else {
                    url = project.spongeReleaseRepo
                }

                if (project.hasProperty('spongeUsername') && project.hasProperty('spongePassword')) {
                    credentials(PasswordCredentials) {
                        username = project.spongeUsername
                        password = project.spongePassword
                    }
                }
            }
        }

    }
}

